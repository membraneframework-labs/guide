# Video Compositor Example

```elixir
File.cd(__DIR__)
Logger.configure(level: :error)

Mix.install([
  {:kino, "~> 0.8.1"},
  {:membrane_core, "~> 0.11.2"},
  {:membrane_kino_plugin, github: "membraneframework-labs/membrane_kino_plugin"},
  {:membrane_file_plugin, "~> 0.13.0"},
  {:membrane_raw_video_format, "~> 0.2"},
  {:membrane_h264_ffmpeg_plugin, "~> 0.25.2"},
  {:membrane_video_compositor_plugin, "~> 0.2.1"},
  {:membrane_hackney_plugin, "~> 0.9.0"},
  {:membrane_realtimer_plugin, "~> 0.6.0"},
  {:membrane_aac_format, "~> 0.7.0"}
])
```

## Pipeline module definition

Defines a video compositor with a `1440x960` output scene, waiting for up to four videos. Each of them is downloaded in real-time from the network using the `Hackney` module, parsed and decoded from the `H264` format, and composed in real-time. After that, they will be displayed using the `Kino.Video` player.

```elixir
defmodule DynamicVideoComposition do
  use Membrane.Pipeline

  alias Membrane.H264.FFmpeg.{Encoder, Parser, Decoder}
  alias Membrane.{Pad, RawVideo, Time}

  alias Membrane.{
    Hackney,
    Kino,
    VideoCompositor
  }

  alias Membrane.VideoCompositor.RustStructs.BaseVideoPlacement, as: VideoPlacement

  @media_url "http://raw.githubusercontent.com/membraneframework/static/gh-pages/samples/big-buck-bunny/bun33s_720x480.h264"

  @framerate {25, 1}

  @output_video_format %RawVideo{
    width: 1440,
    height: 960,
    framerate: @framerate,
    pixel_format: :I420,
    aligned: true
  }

  @placement %VideoPlacement{
    size: {720, 480},
    position: {0, 0},
    z_value: 0.0
  }

  @positions [{0, 0}, {720, 0}, {0, 480}, {720, 480}]

  @impl true
  def handle_init(_ctx, options) do
    structure =
      child(:compositor, %VideoCompositor{stream_format: @output_video_format})
      # Kino player requires H264 frames without b-frames
      |> child(:encoder, %Encoder{profile: :baseline})
      |> via_in(:video)
      |> child(:player, %Kino.Player.Sink{kino: options[:kino]})

    start_time = Time.vm_time()
    state = %{next_video_id: 0, start_time: start_time}

    {[spec: structure, playback: :playing], state}
  end

  @impl true
  def handle_info(:add_video, _ctx, state) when state.next_video_id >= 4 do
    {[], state}
  end

  @impl true
  def handle_info(:add_video, _ctx, state) do
    %{start_time: start_time, next_video_id: next_video_id} = state

    position = Enum.at(@positions, next_video_id)
    placement = %VideoPlacement{@placement | position: position}
    offset = Time.vm_time() - start_time
    video_id = next_video_id

    video_source =
      child({:media_source, video_id}, %Hackney.Source{
        location: @media_url,
        hackney_opts: [follow_redirect: true],
        max_retries: 3
      })
      |> child({:parser, video_id}, %Parser{framerate: @framerate})
      |> child({:decoder, video_id}, Decoder)
      # Hackney source may fetch all frames at once from one source and flood compositor.
      # This could cause undefined behaviour if the first source sent the entire stream 
      # and EOS signal before connecting the other sources.
      # To ensure real-time processing, all streams are synchronized by reducing 
      # processing speed (based on the framerate). 
      |> child({:realtimer, video_id}, Membrane.Realtimer)
      |> via_in(Pad.ref(:input, video_id),
        options: [initial_placement: placement, timestamp_offset: offset]
      )
      |> get_child(:compositor)

    {[spec: video_source], %{state | next_video_id: next_video_id + 1}}
  end
end

:ok
```

## Playing video

Kino video player.

**Remember to press the play button on the player after viewing the first video**

```elixir
kino = Membrane.Kino.Player.new(:video)
```

Activate pipeline and wait for videos:

```elixir
{:ok, _supervisor_pid, pipeline} = Membrane.Pipeline.start(DynamicVideoComposition, kino: kino)
```

One can add up to four video inputs:

```elixir
send(pipeline, :add_video)
```

```elixir
send(pipeline, :add_video)
```

```elixir
send(pipeline, :add_video)
```

```elixir
send(pipeline, :add_video)
```
